apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        applicationId globalConfiguration.getAt("androidApplicationId")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        testInstrumentationRunner globalConfiguration.getAt("testInstrumentationRunner")
        testApplicationId globalConfiguration.getAt("testApplicationId")
        multiDexEnabled false
    }

    signingConfigs {
        release {
            keyAlias 'marcohc'
            keyPassword 'android'
            storeFile file('keystore/keystore.jks')
            storePassword 'android'
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles 'proguard-app.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        dexInProcess true
        preDexLibraries = true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

}

dependencies {

    compile project(':core')
    compile project(':recycler')
    compile project(':rest')
    compile project(':analytics')
    compile project(':security')

    //////////////////////////////////////////////////////////////////////////////////////////////
    // PRESENTATION
    //////////////////////////////////////////////////////////////////////////////////////////////

    // Google
    compile 'com.android.support:appcompat-v7:24.2.1'

    // Butterknife
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // Custom crash
    compile 'cat.ereza:customactivityoncrash:1.5.0'

    // Leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Dagger
    compile 'com.google.dagger:dagger:2.6.1'
    apt 'com.google.dagger:dagger-compiler:2.6.1'

    //////////////////////////////////////////////////////////////////////////////////////////////
    // DATA
    //////////////////////////////////////////////////////////////////////////////////////////////

    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    //////////////////////////////////////////////////////////////////////////////////////////////
    // TESTING
    //////////////////////////////////////////////////////////////////////////////////////////////

    // Instrumentation tests
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'

    // Non instrumentation tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.106-beta'

}